plugins {
    id 'idea'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.johnrengelman.shadow' version '6.0.0' apply false
}

idea {
    project {
        languageLevel = 14
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group 'ru.mherarsh'

    repositories {
        mavenCentral()
    }

    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:2.3.1.RELEASE")
            }
            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("org.reflections:reflections:${Versions.reflections}")
            dependency("org.ow2.asm:asm-commons:${Versions.asm}")
            dependency("org.glassfish:javax.json:${Versions.glassfishJson}")
            dependency("org.openjdk.jmh:jmh-core:${Versions.jmh}")
            dependency("org.openjdk.jmh:jmh-generator-annprocess:${Versions.jmh}")
            dependency("com.google.code.gson:gson:${Versions.gson}")
            dependency("org.eclipse.jetty:jetty-server:${Versions.jetty}")
            dependency("org.eclipse.jetty:jetty-webapp:${Versions.jetty}")
            dependency("org.eclipse.jetty:jetty-security:${Versions.jetty}")
            dependency("org.freemarker:freemarker:${Versions.freemarker}")
            dependency("org.javassist:javassist:${Versions.javassist}")
            dependency("org.webjars:sockjs-client:${Versions.sockjs}")
            dependency("org.webjars:stomp-websocket:${Versions.stomp}")
            dependency("org.webjars:jquery:${Versions.jquery}")
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

